plugins {
    id 'de.dfki.mary.voicebuilding-legacy' version '5.2.0'
    id 'de.undercouch.download' version '3.1.0'
}

group 'de.dfki.mary'
version '5.2-SNAPSHOT'

voice {
    name = 'upmc-pierre'
    language = 'fr'
    gender = 'male'
    type = 'unit selection'
    description = 'A male French unit selection voice, built at ISIR (UPMC) using data recorded by Pierre Chauvin'
    license {
        name = 'Creative Commons Attribution-ShareAlike 3.0 Unported'
        shortName = 'CC-BY-SA'
        url = 'http://mary.dfki.de/download/by-sa-3.0.html'
    }
    samplingRate = 16000
}

import de.undercouch.gradle.tasks.download.Download

task downloadAudio(type: Download) {
    ext.destFile = file("$buildDir/audio.tar")
    src "https://github.com/marytts/upmc-pierre-data/releases/download/v0.1/$destFile.name"
    dest destFile.parentFile
    overwrite false
    acceptAnyCertificate true
}

task unpackAudio(type: Copy) {
    dependsOn downloadAudio
    from tarTree(downloadAudio.destFile)
    into buildDir // tarball unpacks build/audio actually
}

task downloadData(type: Download) {
    ext.destFile = file("$buildDir/upmc-pierre-data.zip")
    src "https://github.com/marytts/upmc-pierre-data/archive/v0.1.zip"
    dest destFile
    overwrite false
    acceptAnyCertificate true
}

task unpackData(type: Copy) {
    dependsOn downloadData
    from zipTree(downloadData.destFile)
    into buildDir
    include '**/*.txt', '**/*.lab'
    filesMatching '**/*.txt', {
        it.path = "text/$it.name"
    }
    filesMatching '**/*.lab', {
        it.path = "lab/$it.name"
    }
    includeEmptyDirs = false
}

processDataResources.dependsOn unpackAudio, unpackData

wav {
    srcDir = file("$buildDir/audio")
    srcFileExt = 'flac'
}
